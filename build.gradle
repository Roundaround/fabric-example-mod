plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

def groupId = project.maven_group
def modId = project.archives_base_name
def modVersion = project.mod_version
def fullVersion = "${modVersion}-${project.minecraft_version}"

loom {
	addRemapConfiguration("remapRoundaLibShade", {
		targetConfigurationName = "roundaLibShade"
		sourceSet = sourceSets.main
		onCompileClasspath = true
		onRuntimeClasspath = true
	})
}

configurations.remapRoundaLibShade {
	transitive = false
}

repositories {
	maven {
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		url = "https://maven.rnda.dev/releases/"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

	modImplementation "me.roundaround:roundalib:${project.roundalib_version}"
	remapRoundaLibShade "me.roundaround:roundalib:${project.roundalib_version}"
}

base {
	archivesName = project.archives_base_name
}

processResources {
	inputs.property "version", modVersion

	filesMatching("fabric.mod.json") {
		expand "version": modVersion
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release.set(17)
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}

/**
 * Custom stuff here
 */
remapJar {
	archiveBaseName.set(modId)
	archiveVersion.set(fullVersion)
	archiveClassifier.set("remapped")
}

shadowJar {
	dependsOn remapJar
	from tasks.remapJar.outputs.files

	configurations = [project.configurations.remapRoundaLibShade]
	relocate "me.roundaround.roundalib", "${groupId}.${modId}.roundalib"
	exclude "**/assets/roundalib/**"
	exclude "**/roundalib-refmap.json"
	exclude "**/roundalib.mixins.json"

	archiveBaseName.set(modId)
	archiveVersion.set(fullVersion)
	archiveClassifier.set("")
}

build {
	dependsOn shadowJar
}
/**
 * End custom stuff
 */

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
